#version 430

layout(local_size_x = 1, local_size_y = 1) in;
layout(r32f, binding = 1) uniform image2D img_data;

uniform dvec2 fractalTL;
uniform double xScale;
uniform double yScale;
uniform int iterations;

void main() {

	dvec2 id = gl_GlobalInvocationID.xy;

	dvec2 scaledOffset = dvec2(id.x * xScale, id.y * yScale );
	dvec2 fractalCoord = dvec2(fractalTL + scaledOffset);

	dvec2 z = dvec2(0.0, 0.0);
	dvec2 c = fractalCoord;

	int n = 0;
	for (; n < iterations && dot(z, z) < 4.0; n++)
	{
        double zRealTmp = z.x;
        z.x = (zRealTmp * zRealTmp) - (z.y * z.y) + c.x;
        z.y = 2.0 * zRealTmp * z.y + c.y;
	}

	imageStore(img_data, ivec2(id), vec4(n, 0.0, 0.0, 1.0));
}
